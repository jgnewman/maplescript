(import 'react'            React)
(import 'react-dom'        ReactDOM)
(import 'redux'            [bindActionCreators])
(import 'react-redux'      [connect])
(import '../state/actions' [:actions])
(import '../util/reactify' [:reactify])
(import '../Logo'          [:Logo])
(import '../Nav'           [:Nav])
(import '../HomeContent'   [:HomeContent])
(import '../Menu'          [:Menu])
(import '../CodeSample'    [:CodeSample])

(import '../handlers/handlers' [
  handleClickNav
  handleSelectLink
  handleClickMenuLink
  handleClickLogo
])


###
@private select - Used with `connect` to add state properties to the container
                  as props.
@param   {Object} state  The current redux state.
@returns {Object}        Class properties mapped to the redux state.
###
(make select [state] {
  menuOpen state.menuOpen
  downloadOptions state.downloadOptions
  downloadSelection state.downloadSelection
})

###
@private action - Used with `connect` to add actions to the container
                  as props.
@param   {Function} dispatch  A function for dispatching redux actions.
@returns {Object}             Action dispatching functions mapped to class properties.
Example:
{ propName: (bindActionCreators actions.fnName, dispatch) }
###
(make action [dispatch] {
  toggleMenu (bindActionCreators actions.toggleMenu dispatch)
  selectLink (bindActionCreators actions.selectLink dispatch)
})


###
@public render - A render function for the React class.
@returns {ReactElement} Used to render the app.
###
(make render []

  (destr @props [
    menuOpen
    toggleMenu
    downloadOptions
    downloadSelection
    selectLink
  ])

  (React.createElement 'div' {} [

    (React.createElement Logo {
      menuOpen menuOpen
      toggleMenu toggleMenu
      handleClickLogo handleClickLogo
    })

    (React.createElement Nav {
      menuOpen menuOpen
      toggleMenu toggleMenu
      handleClickNav handleClickNav
    })

    (React.createElement HomeContent {
      menuOpen menuOpen
      downloadOptions downloadOptions
      downloadSelection downloadSelection
      handleSelectLink handleSelectLink
      selectLink selectLink
    })

    (React.createElement Menu {
      menuOpen menuOpen
      downloadOptions downloadOptions
      downloadSelection downloadSelection
      handleSelectLink handleSelectLink
      handleClickMenuLink handleClickMenuLink
      selectLink selectLink
      toggleMenu toggleMenu
    })

    (React.createElement CodeSample {})
  ])
)

# Create and export a connected component
(make connected (>>= connect [select action] [(reactify {render render} {})]))
(export connected)
